#Source files
engine = Glob("engine/*.cc", strings=True)
characters = Glob("characters/*.cc", strings=True)
backend = engine + characters
bmpout = "bmp.cc"
gufg = "game.cc"



base = Environment(CXXFLAGS=["-O2",
                             "-std=c++11",
                             "-g", "-Wall"],
                   LIBS=['SDL',
                         'SDLmain',
                         'SDL_image',
                         'SDL_mixer',
                         'glut'])

gcc = base.Clone()
gcc.Replace(CXX="g++")
gcc.Append(LIBS=['GL', 'GLU'],
           CXXFLAGS=["-Wno-parentheses",
                     "-Wno-unused-but-set-variable"])


clang = base.Clone()
clang.Replace(CXX="clang++")
clang.Append(LIBS=['GL', 'GLU', 'm', 'c++'],
             CXXFLAGS=["-stdlib=libc++"])

for k, env in {"":gcc, ".cl":clang}.items():
  b = [env.Object(name+k+".o", name) for name in backend]
  env.Program("../gufg"+k, b + env.Object(gufg+k+".o", gufg))
  env.Program("../bmpout"+k, b + env.Object(bmpout+k+".o", bmpout))

Default("../gufg")
